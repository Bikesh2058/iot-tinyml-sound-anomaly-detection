#include <Arduino.h>
#include <ArduinoBLE.h>
#include <WiFiS3.h>
#include "Iot_Project_inferencing.h" // Edge Impulse quantized library

// User Settings
#define MIC_PIN A0
#define LED_PIN 7
#define BUZZER_PIN 13
#define THRESHOLD 0.7 // Anomaly threshold

// BLE Setup
BLEService anomalyService("12345678-1234-5678-1234-56789ABCDEF0");

BLEStringCharacteristic anomalyChar(
  "ABCDEF01-1234-5678-1234-56789ABCDEF0",
  BLERead | BLENotify,
  20
);

// WiFi Setup
char WIFI_SSID[] = "VM20098";
char WIFI_PASS[] = "1234567%&8";
const char* THINGSPEAK_SERVER = "api.thingspeak.com";
const char* THINGSPEAK_API_KEY = "1730U531SJC5KD4B";

WiFiClient tsClient;

// Audio buffer (int8 for quantized model)
static int8_t audio_buffer[EI_CLASSIFIER_RAW_SAMPLE_COUNT];

// Edge Impulse: Provide audio data
int my_get_data(size_t offset, size_t length, float *out_ptr) {
    if (offset + length > EI_CLASSIFIER_RAW_SAMPLE_COUNT) return -1;
    for (size_t i = 0; i < length; i++) {
        out_ptr[i] = (float)audio_buffer[offset + i] / 128.0f;
    }
    return 0;
}

// Send data to ThingSpeak
void sendToThingSpeak(bool anomalyDetected) {
    if (WiFi.status() != WL_CONNECTED) return;
    if (tsClient.connect(THINGSPEAK_SERVER, 80)) {
        String url = "/update?api_key=";
        url += THINGSPEAK_API_KEY;
        url += "&field1=";
        url += anomalyDetected ? "1" : "0"; // Send 1 for anomaly, 0 for normal
        tsClient.print("GET " + url + " HTTP/1.1\r\n" +
                       "Host: api.thingspeak.com\r\n" +
                       "Connection: close\r\n\r\n");
        tsClient.stop();
    }
}

// Setup
void setup() {
    Serial.begin(115200);
    pinMode(MIC_PIN, INPUT);
    pinMode(LED_PIN, OUTPUT);
    pinMode(BUZZER_PIN, OUTPUT);

    // BLE setup
    if (!BLE.begin()) {
        Serial.println("BLE failed to start");
        while (1);
    }

    BLE.setLocalName("TinyML_Anomaly");
    BLE.setAdvertisedService(anomalyService);

    anomalyService.addCharacteristic(anomalyChar);
    BLE.addService(anomalyService);
    
    anomalyChar.writeValue("NORMAL");
    BLE.advertise();
    Serial.println("BLE advertising started");

    // WiFi setup
    Serial.print("Connecting to WiFi");
    WiFi.begin(WIFI_SSID, WIFI_PASS);
}

// Main loop
void loop() {

    // Audio Sampling
    for (size_t i = 0; i < EI_CLASSIFIER_RAW_SAMPLE_COUNT; i++) {
        int raw = analogRead(MIC_PIN);
        audio_buffer[i] = map(raw, 0, 4095, -128, 127);
    }

    // Run inference
    signal_t signal;
    signal.total_length = EI_CLASSIFIER_RAW_SAMPLE_COUNT;
    signal.get_data = &my_get_data;

    ei_impulse_result_t result;
    EI_IMPULSE_ERROR ei_err = run_classifier(&signal, &result, false);

    // Determine anomaly
    float currentReading = 0.0;
    for (size_t i = 0; i < EI_CLASSIFIER_LABEL_COUNT; i++) {
        if (strcmp(result.classification[i].label, "anomaly") == 0) {
            currentReading = result.classification[i].value; // Get the live reading
            break;
        }
    }

    // LED + Buzzer
    bool anomalyDetected = currentReading >= THRESHOLD;
    digitalWrite(LED_PIN, anomalyDetected ? HIGH : LOW);
    digitalWrite(BUZZER_PIN, anomalyDetected ? HIGH : LOW);

    // Simple uptime clock (hh:mm:ss)
    unsigned long totalsecs = millis() / 1000;
    int hr = totalsecs / 3600;
    int min = (totalsecs % 3600) / 60;
    int sec = totalsecs % 60;

    char buffer[70];
    sprintf(buffer, "[%02d:%02d:%02d] Reading: %.2f | Threshold: %.2f | Status: %s.", 
    hr, min, sec, currentReading, THRESHOLD, anomalyDetected ? "ANOMALY DETECTED" : "NORMAL");
    Serial.println(buffer);

    // BLE + ThingSpeak
    static bool lastState = false;

    if (BLE.connected()) {
        if (anomalyDetected && !lastState) {
            anomalyChar.writeValue("ANOMALY DETECTED!");
        }
        lastState = anomalyDetected;
    }
    sendToThingSpeak(anomalyDetected);


    // BLE events
    BLE.poll();
    delay(1500);
}
